// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    //Zero out x & y inputs, pass onto x & y not gates
    //zx
    Mux16 (a[0..15]=x[0..15], b[0..15]=false, sel=zx, out=xzerod);

    //zy
    Mux16 (a[0..15]=y[0..15], b[0..15]=false, sel=zy, out=yzerod);

    //X, Y, & F not gates
    //nx
    Not16 (in=xzerod, out[0..15]=xnot);
    Mux16 (a=xzerod, b=xnot, sel=nx, out=xnotd);

    //ny
    Not16 (in[0..15]=yzerod, out[0..15]=ynot);
    Mux16 (a[0..15]=yzerod, b[0..15]=ynot, sel=ny, out[0..15]=ynotd);

    //F gate.
    //And16 and Add16, results passed to Mux16
    Add16 (a[0..15]=xnotd, b[0..15]=ynotd, out[0..15]=fadd);
    And16 (a[0..15]=xnotd, b[0..15]=ynotd, out[0..15]=fand);
    Mux16 (a[0..15]=fand, b[0..15]=fadd, sel=f, out[0..15]=fout);

    //no, includes sending out[15] to ng
    Not16 (in[0..15]=fout, out[0..15]=notf);
    Mux16 (a[0..15]=fout, b[0..15]=notf, sel=no, out[0..15]=out, out[0..7]=or8in1, out[8..15]=or8in2, out[15]=ng);

    //zr return
    Or8Way (in[0..7]=or8in1, out=or8out1);
    Or8Way (in[0..7]=or8in2, out=or8out2);
    Or (a=or8out1, b=or8out2, out=oroutzr);
    Not (in=oroutzr, out=zr);
}